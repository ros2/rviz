# Use custom CMake config files for assimp on Ubuntu to work around a cmake warning.
# This should be removed once upstream is updated to assimp-0.5.1, see:
#   https://github.com/ros2/rviz/issues/524
#   https://bugs.launchpad.net/ubuntu/+source/assimp/+bug/1869405
### BEGIN HACKS
set(IS_UBUNTU FALSE)
if(UNIX AND NOT APPLE)
  find_program(LSB_RELEASE_EXEC lsb_release)
  if(EXISTS "${LSB_RELEASE_EXEC}")
    execute_process(COMMAND ${LSB_RELEASE_EXEC} -is
        OUTPUT_VARIABLE LSB_RELEASE_ID_SHORT
        OUTPUT_STRIP_TRAILING_WHITESPACE
    )
  else()
    set(LSB_RELEASE_ID_SHORT "Unknown")
  endif()

  if(${LSB_RELEASE_ID_SHORT} STREQUAL "Ubuntu")
    set(IS_UBUNTU TRUE)
  endif()
endif()

if(IS_UBUNTU)
  set(assimp_DIR "${@PROJECT_NAME@_DIR}/../../../opt/rviz_assimp_vendor_custom_config")
endif()
### END HACKS

find_package(assimp QUIET)

if(NOT assimp_FOUND OR "${assimp_VERSION}" VERSION_LESS 4.1.0)
  # add the local Modules directory to the modules path
  set(assimp_DIR "${@PROJECT_NAME@_DIR}/../../../opt/rviz_assimp_vendor/lib/cmake/assimp-4.1")
  message(STATUS "Setting assimp_DIR to: '${assimp_DIR}'")
endif()

find_package(assimp REQUIRED QUIET)

# workaround bug in Assimp 4.1.0 https://github.com/assimp/assimp/issues/1914
# Affecting Ubuntu package: libassimp-dev 4.1.0~dfsg-3, Brew: assimp 4.1.0
string(REPLACE "/lib/lib/" "/lib/" ASSIMP_LIBRARY_DIRS "${ASSIMP_LIBRARY_DIRS}")
string(REGEX REPLACE "/lib/include$" "/include" ASSIMP_INCLUDE_DIRS "${ASSIMP_INCLUDE_DIRS}")

set(rviz_assimp_vendor_LIBRARIES)
foreach(library IN LISTS ASSIMP_LIBRARIES)
  message(STATUS "library: ${library}")
  if(IS_ABSOLUTE "${library}")
    list(APPEND rviz_assimp_vendor_LIBRARIES "${library}")
  else()
    find_library(library_abs ${library} PATHS "${ASSIMP_LIBRARY_DIRS}" NO_DEFAULT_PATH)
    list(APPEND rviz_assimp_vendor_LIBRARIES "${library_abs}")
  endif()
endforeach()
set(rviz_assimp_vendor_LIBRARY_DIRS ${ASSIMP_LIBRARY_DIRS})
set(rviz_assimp_vendor_INCLUDE_DIRS ${ASSIMP_INCLUDE_DIRS})
