# prevent multiple inclusion
if(_@PROJECT_NAME@_CONFIG_INCLUDED)
  return()
endif()
set(_@PROJECT_NAME@_CONFIG_INCLUDED TRUE)

# output package information
if(NOT @PROJECT_NAME@_FIND_QUIETLY)
  message(STATUS "Found @PROJECT_NAME@: @PACKAGE_VERSION@ (${@PROJECT_NAME@_DIR})")
endif()

find_package(assimp QUIET)

if(NOT assimp_FOUND)
  # add the local Modules directory to the modules path
  if(WIN32)
    set(assimp_DIR "${@PROJECT_NAME@_DIR}/../../../opt/rviz_assimp_vendor/CMake")
  else()
    set(assimp_DIR "${@PROJECT_NAME@_DIR}/../../../opt/rviz_assimp_vendor/lib/cmake/assimp-4.0")
  endif()
  message(STATUS "Setting assimp_DIR to: '${assimp_DIR}'")
endif()

find_package(assimp REQUIRED QUIET)

set(rviz_assimp_vendor_LIBRARIES)
foreach(library IN LISTS ASSIMP_LIBRARIES)
  message(STATUS "library: ${library}")
  if(IS_ABSOLUTE "${library}")
    list(APPEND rviz_assimp_vendor_LIBRARIES "${library}")
  else()
    find_library(library_abs ${library} PATHS "${ASSIMP_LIBRARY_DIRS}" NO_DEFAULT_PATH)
    list(APPEND rviz_assimp_vendor_LIBRARIES "${library_abs}")
  endif()
endforeach()
set(rviz_assimp_vendor_LIBRARY_DIRS ${ASSIMP_LIBRARY_DIRS})
set(rviz_assimp_vendor_INCLUDE_DIRS ${ASSIMP_INCLUDE_DIRS})
